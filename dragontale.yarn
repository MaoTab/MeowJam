title: Node_NULL
position: -2924,-1392
---
===

title: Node_对话
position: -2937,-1220
---
<<clear_dlg>>
<<show_event "开始事件" "冲冲冲！">>
System:这里是DEBUG中转站，测试一个数值嵌入文本，当前的SOC等级是：{format_invariant(prism_get("soc"))}[continue/]
->进入测试环节[check dc=10 name="你需要roll中才能进测试"/]
    <<black>>
    <<move_to 30 45>>
    <<show_event "测试环节" "狠狠地测事！">>
    <<jump Node_测试>>
->序章
    <<jump Node_序章>>
===

title: Node_测试
position: -2413,-1410
---
->等级修改
    <<jump Node_等级修改测试>>
->投票测试
    <<jump Node_投票测试>>
->死一死
    <<jump Node_测试_死亡>>
->返回
    <<jump Node_对话>>
===

title: Node_测试_死亡
position: -2039,-1440
---
<<clear_dlg>>
<<dead_wait>>

Maot:还要再死一次吗？[continue/]
->来
    <<jump Node_测试_死亡>>
->返回
    <<jump Node_测试>>
===

title: Node_等级修改测试
position: -2230,-1192
---
->\[color=a8d976\]+1 BIO\[/color\]
    <<prism_add "bio" 1>>
->\[color=a8d976\]+1 PSY\[/color\]
    <<prism_add "psy" 1>>
->\[color=a8d976\]+1 SOC\[/color\]
    <<prism_add "soc" 1>>
->\[color=ff6188\]-1 BIO\[/color\]
    <<prism_add "bio" -1>>
->\[color=ff6188\]-1 PSY\[/color\]
    <<prism_add "psy" -1>>
->\[color=ff6188\]-1 SOC\[/color\]
    <<prism_add "soc" -1>>
->返回
    <<jump Node_测试>>

<<jump Node_等级修改测试>>
===

title: Node_投票测试
position: -2648,-1157
---
// 投票真他妈是个genius的点子啊！

<<clear_dlg>>
<<vote>>

<<declare $votes = "soc">>

Maot:你要支持谁？[continue/]
->感情[vote t="soc"/]
    <<set $votes = "soc">>
->理性[vote t="psy"/]
    <<set $votes = "psy">>
->肉体[vote t="bio"/]
    <<set $votes = "bio">>

<<if prism_vote($votes) == "soc">>
    <<vote_over "soc">>
    Maot:感情赢得了票选。
<<elseif prism_vote($votes) == "psy">>
    <<vote_over "psy">>
    Maot:理性赢得了票选。
<<elseif prism_vote($votes) == "bio">>
    <<vote_over "bio">>
    Maot:肉体赢得了票选。
<<endif>>

Maot:还要再来一次吗？[continue/]
->来
    <<jump Node_投票测试>>
->返回
    <<jump Node_测试>>
===

title: Node_序章
position: -2940,-1005
---
<<clear_dlg>>
<<show_event "泡沫" "陷入雪景球之中。">>
<<show_ill "player" "char">>
<<dlg_mode "perception">>

肉体:严寒，手上握着金属铁棍，这是。。登山杖？你在雪山上，狂风试图摧毁你那裹得严严实实的登山服。饥饿干渴带着寒冷与困顿，将你的身体一片不剩的尽数摧毁。你感觉很困，就像是睡在几十米宽的泡泡床上，慢慢的的陷入其中或许是一种好选择。

情感:你最后的记忆是劳伦医生寄来一纸书信，附带寄来一种名作“气泡果”的特产。

理性:天色已晚，目前我们正在尝试下山，目前发生的情况无法以任何科学的方式进行解释，我只能认为我们正在做一场过于真实的幻梦，且仍未醒来。

<<dlg_mode "discussion">>

理性:再多做一丝无用的思考，多消耗一丝大脑的糖与氧气都会让你生还的机会更低一级。

肉体:哦……不，那是极光吗？我们难道登顶了？

理性:我们正在下山。

情感:这只是你带着好奇，用针戳了一下这泡沫。

肉体:然后我们就在这该死的雪山上了！

理性:我们目前无法测量这座山到底有多高，只能尽可能地往下走。

情感:等等，我看见一个人影，那是劳伦医生吗？

理性:那只是一卷泡沫，一颗无用的泡影。

肉体:只要睡下，就可以享受永久的安逸。

情感:不！我很确定！我们如果能够得到她的协助……

理性:接着往前走，不要去关注那些精神失常所带来的幻觉。

肉体:快看呐！那天空好似一个……一个，一戳！一戳就破的气泡！那雪山顶的灯塔就像那一根针！

情感:嘿！我们有希望了，她好像还带着一艘船，可以在这雪山上快速行驶。

理性:失控了，我也看见了船与灯塔，而且是在雪山上。快拉不住这脱缰的缰绳了。最终我们都将命绝于此？

<<show_ill "npc" "char">>

情感:喔！你不是劳伦，你是……不似于它的某种存在？我该称呼祢什么，神？上帝？高天？

理性:不论祢是谁，我向祢祈求，请为这残破的身躯划定道标，它孱弱、黯淡，宛若一碰就碎的气泡，但我们仍不应该绝于此。

肉体:祢像一根尖针，轻柔地扎进了这肥皂泡之中，而没有伤其分毫。我也向祢祈求，让我安眠，结束这一切痛苦。

情感:我相信你的判断，不论祢做何选择。
<<vote>>

->\[color=ff6188\]让他安眠，沉入这温暖的泡沫之中。\[/color\][vote t="bio"/] 
    <<set $votes = "bio">>
->\[color=6dc0ca\]咬碎满口牙，坚持继续下山。\[/color\][vote t="psy"/]
    <<set $votes = "psy">>

<<if prism_vote($votes) == "psy">>
    <<vote_over "psy">>
    <<jump Node_序章_下山>>
<<elseif prism_vote($votes) == "bio">>
    <<vote_over "bio">>
    <<jump Node_序章_安眠>>
<<endif>>
===


title: Node_序章_安眠
position: -3049,-762
---
<<dlg_mode "do">>

肉体:你安眠了，在这刺骨的寒风也开始温柔地嘘寒问暖，快看呐，天空就像是倒扣过来的一个玻璃盖，不，一个巨大的肥皂泡！闪烁着耀眼的彩光！

情感:我们会前往，满是泡沫的天堂吗？哦，那山顶的灯塔会把它刺破吗？

理性:肥皂泡碎了，很显然，在那之后只剩下永恒的死寂——而我们也不再有梦。

肉体:夏天的雪，落在身上竟然如此温暖。

<<black>>
<<prism_add "bio" "1">>
<<prism_add "self" "1">>
<<jump Node_第一章_城镇>>
===

title: Node_序章_下山
position: -3331,-748
---
<<dlg_mode "do">>

肉体:在这漫漫长夜之下，伴随你的仅有寒风呼啸。登山服已经沾满了不知从何而来的泡沫，你的步伐越走越沉，它们像是在拖着你，像海浪，不断的拍打你试图耗尽你的每一丝体力。

理性:最后把你吞进那咸苦的沫子里。

情感:远处似乎有一些带着玩具铲子上山的镇民。

理性:但你无法判别那是否是另一个幻觉，在这下山的路上你已经遇上很多不真实的"人"了。

情感:我也不能保证下一个就能是真的，但……万一呢？

理性:那也不过是另一个幻梦。

肉体:手脚开始变得越来越暖和了，甚至有些发烫，你恨不得脱掉每一件登山服。

理性:失温症。

情感:那灯塔会把穹顶刺破！我们得把这件事情告诉那群小人儿……

<<black>>
<<prism_add "psy" "1">>
<<prism_add "self" "1">>
<<jump Node_第一章_城镇>>
===

title: Node_第一章_城镇
position: -3190,-487
---
<<clear_dlg>>
<<show_event "城镇" "XXXXX">>
<<dlg_mode "perception">>

肉体:最先纳入感知的是干燥的空气与唇膏的触感，身体陷落在松软的白床上。睁开双眼，眼前是一片洁白的天花板，空气中弥漫着消毒水的气味。放松的感觉宛如融化的热巧克力一般，让你瘫软在这靠着暖炉与白洁窗户的病床上。窗外那标志性的雪山，以及近处挂着气泡果的针叶树无不提醒你，这里是“霜湾”，医生的故居，也是气泡果的特产地。

情感:当时是医生带着搜救队上山，搜救48小时将倒栽在雪地里的你硬生生铲出来。你已经昏迷了一整周，她也陪护了你一整周。据她所称，你像是变了个人，不同以往的优柔寡断，而是突然显得尤为自由洒脱。

理性:但那非同寻常的雪与宛如气泡一般的穹顶却在你的记忆中挥之不去，山顶的灯塔，那刺破薄膜的尖针此刻却化作恍惚的虚像，你侧头向窗外望去那里便是雪山，但那所谓的灯塔却不见踪影。

情感:医生还是一如既往的体贴，足够安抚你那由于剧变而恐惧不安的心。

<<dlg_mode "discussion">>

理性:所以，为什么我们会出现在雪山上？

肉体:是劳伦医生的“气泡果”，自从你刺破它的一瞬间就到了山上。

情感:气泡果是霜湾的特产，它内含的气体具有一定的镇定以及轻微的“致幻”效果。

理性:它产自本地的所有针叶树上，不分品种。

肉体:与其说是"长出来"，它们更像是"落在树上"的——没有前因后果，仿佛只是突然且偶然的“出现”在这里一般。

肉体:恢复的状况远比想象中的要快不少，本来深度冻伤应该需要康复很久很久。你就像一颗朝天生长的树，不断的成长，仿佛想要刺破天空。

情感:霜湾也是著名的登山胜地，至今仍未有人成功登顶过那最后的峭壁，无数有志之人无数次对最终的登顶发起冲击，但就结果而言，死伤人数几乎难以计量。

理性:但这仍然远无法解释为何我们此刻会躺在这病床上，而那该死的医生又到底去了哪，这一切的一切都欠缺一个合理的缘由。

// （判断：等级1）
肉体:不过我能够感受到体内那层层叠叠的脉动，燥热的感觉始终挥之不去，有某种成长出现在身体里，而且这必然是永久性的。

理性:不论如何，我必须得找到劳伦，当场质问她，不管是我们如何到这，更是那些光怪陆离的幻觉。针与泡沫就宛如可憎的钉子，在你的意识中深埋，不得不去想它们。（提案搜寻劳伦）

情感:还记得上一次登山吗，你们就像是喝醉酒一般倒向彼此，但在登顶阶段你们靠着相互鼓励成功将一个个岩釘敲进那不通人情的岩壁里。她是如此的可靠，也是如此的依赖你。与其说她是你的主治医师，你们更像是彼此的病人。或许我们应该去找她，把既往的遭遇解释清楚。（提案与劳伦叙旧）

肉体:我们必须得尽快离开这座小镇，上一次登顶所的痛苦我是不想要再体验一次了。那可是所有山峰里，"最低"的那一座。（提案离开）

神性:它此刻正在主峰顶端俯视着你，那里不只有灯塔，灯塔下应该有一扇门，门后不只有一扇门。门的终点也应该在穷极一切之后，在那之前，你至少得戳破这层穹顶。（提案登山）

你:你所说的门，气泡，灯塔，到底是什么？

神性:它们对应着考验，蒙蔽，破局者。
<<jump Node_第一章_城镇_分支1>>
===

title: Node_第一章_城镇_分支1
position: -3189,-280
---
->你是谁?
    <<jump Node_第一章_城镇_谁>>
->你是为了? // MAKR
    <<jump Node_第一章_城镇_疑惑>>
->明确拒绝回应。
    <<jump Node_第一章_城镇_拒绝>>
->完全沉默，不作回应。
    <<jump Node_第一章_城镇_沉默>>
===

title: Node_第一章_城镇_谁
position: -4009,-46
---
神性:我只是他内心深处的一个微弱回响，源于你真理的渴望。我的存在完全依托于他的信念。
<<jump Node_第一章_城镇_分支1>>
===

title: Node_第一章_城镇_疑惑
position: -3763,-50
---
神性:请原谅我的唐突，我并非想要左右你的选择。只是作为您忠实的虔信者，我不得不提醒你要当心那三重性格带来的困扰。我在这条求索的道路上，以您为最终的道标与灯塔前进。

===

title: Node_第一章_城镇_拒绝
position: -3470,-52
---
你:你的提议不切实际且光怪陆离。
神性:的确，但我不过是在阐述答案，你现在之所以会有这种感知也是由于你客观所获取的信息不足，气泡外的神明，在这场旅途中你会逐渐接触并理解这一切。

===

title: Node_第一章_城镇_沉默
position: -3189,-45
---
神性:我理解并尊重您的选择，当您需要执行者的时候，这份微弱的信念永存于此。
===